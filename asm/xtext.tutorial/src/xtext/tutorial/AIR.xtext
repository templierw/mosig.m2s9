grammar xtext.tutorial.AIR with org.eclipse.xtext.common.Terminals

generate aIR "http://www.tutorial.xtext/AIR"

// R1 : Racine du fichier
Model:
    (includes+=Includes)*(planes+=Plane | airports+=Airport)*(airlines+=Airline)*;

// R2 : Declaration des inclusions
//la variable « includes » contiendra la liste de tous
//les fichiers déclarés comme inclus dans ce fichier ;
Includes:
    '%include''<'importURI=INCLUDE'>;';

// R3 : Declaration des avions
Plane:
    'Avion' name=ID ':'
    ('Passagers'':'passengers=INT ';'
&    'Motorisation'':'motorisation=Engines';')
    'End.';

// R4 : Declaration des aeroports
Airport:
    'Aeroport' title=STRING ':'
    ('OACI'':' name=CodeOACI';'
&    'Pays'':'country=STRING';'
&    'Pistes'':'runways=INT';')
    'End.';

// R5 : Declaration des lignes aeriennes
Airline:
    'Ligne' name=STRING ':'
    ('Avion'':'plane=[Plane]';'
&    'Compagnie'':'company=STRING';'
&    'Depart'':' departure=[CodeOACI|OACI]';'
&    'Arrivee'':'arrival=[CodeOACI|OACI]';'
&    'Duree'':' duration=DURATION';'
&    regular?='REGULIERE;'?)
    'End.';
    
//la variable « regular » recevra « true » si l'utilisateur entre 'REGULIERE' dans son fichier.


// R6 : Enumeration des motorisations
enum Engines:
  PROPELLER ='HELICES' | ENGINE = 'PROPULSEURS'    ;

// R7 : Duree de vol
terminal DURATION : (('0'..'2''0'..'9')'h')?('0'..'5''0'..'9')'m';

// R8 : Declaration du code OACI
CodeOACI : name=OACI;

terminal OACI : ('A'..'Z')('A'..'Z')('A'..'Z')('A'..'Z');

// R9 : Reconnaissance des fichiers inclus
terminal INCLUDE: ID('.air');
